apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.kassisdion.nestedworld"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        vectorDrawables.useSupportLibrary = true
        resConfigs ("en", "fr")

        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/rxjava.properties'
    }

    dexOptions {
        javaMaxHeapSize "12g"
    }

    buildTypes {
        release {
            buildConfigField "com.nestedworld.nestedworld.data.network.NetworkConstant.Environement", "ENVIRONMENT", "com.nestedworld.nestedworld.data.network.NetworkConstant.Environement.PROD"
            resValue "string", "app_name", "Nestedworld"
            buildConfigField "Boolean", "LOG", 'false'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {//Same as dev (without app_name and environment)
            buildConfigField "Boolean", "LOG", 'true'
            versionNameSuffix "-SNAPSHOT"
        }
        with_back_dev {
            initWith debug // inherit from debug (attribute and signing configurations).
            buildConfigField "com.nestedworld.nestedworld.data.network.NetworkConstant.Environement", "ENVIRONMENT", "com.nestedworld.nestedworld.data.network.NetworkConstant.Environement.DEV"
            resValue "string", "app_name", "Nestedworld Dev"
        }
        with_back_prod {
            initWith debug // inherit from debug (attribute and signing configurations).
            buildConfigField "ccom.nestedworld.nestedworld.data.network.NetworkConstant.Environement", "ENVIRONMENT", "com.nestedworld.nestedworld.data.network.NetworkConstant.Environement.PROD"
            resValue "string", "app_name", "Nestedworld Prod"
        }
    }

    flavorDimensions("environement")
    productFlavors {
        dev {
            applicationIdSuffix = ".dev"
            dimension "environement"
        }
        prod {
            dimension "environement"
        }
    }

    //Special rules for allowing only some buildType/flavor pair
    //Loop over variants
   variantFilter { variant ->
       //Loop over flavor
        variant.getFlavors().each { flavor ->
            //If we build a build dev flavor, we just want to ignore the "default" buildType
            if (flavor.name == 'dev') {
                //If it's a "default" buildtype, ignore it
                if (variant.buildType.name == 'debug' || variant.buildType.name == 'release') {
                    variant.setIgnore(true)
                }
            }
            //If we build a build prod flavor, we only a back_prod buildType
            else if (flavor.name == 'prod') {
                //If it's not a back_prod buildType, ignore it
                if (variant.buildType.name != 'with_back_prod') {
                    variant.setIgnore(true)
                }
            }
        }
    }
}
////////////////////////////////////////////////////////////////////////////////////////////////////
greendao {
    schemaVersion 2
}
////////////////////////////////////////////////////////////////////////////////////////////////////
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    //GPS
    implementation 'com.google.android.gms:play-services-maps:11.6.2'

    //Annotation replacing findViewById()
    implementation "com.jakewharton:butterknife:$rootProject.ext.butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknifeVersion"

    //Support library
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"

    //Material design support helper
    implementation 'com.github.rey5137:material:1.2.4'
    implementation 'com.avast:android-styled-dialogs:2.3.3'

    //Rounded image (with border)
    implementation 'de.hdodenhof:circleimageview:2.1.0'

    //API
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"//send request
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"//use Json converter
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okHttpVersion"//display log
    implementation "com.squareup.okhttp3:okhttp:$rootProject.ext.okHttpVersion"

    //ORM
    implementation 'org.greenrobot:greendao:3.2.0'

    //MessagePack (real time API)
    implementation 'org.msgpack:msgpack-core:0.8.11'

    //Customize typeface
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'

    //Image management
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'jp.wasabeef:glide-transformations:2.0.2'//rounded image

    //Public / Subsbrice to event
    implementation 'org.greenrobot:eventbus:3.0.0'

    //multi dex
    implementation 'com.android.support:multidex:1.0.2'

    //Pie chart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.1'

    //AWS
    implementation "com.amazonaws:aws-android-sdk-core:$rootProject.ext.awsVersion"
    implementation "com.amazonaws:aws-android-sdk-s3:$rootProject.ext.awsVersion"
    implementation "com.amazonaws:aws-android-sdk-ddb:$rootProject.ext.awsVersion"
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
}