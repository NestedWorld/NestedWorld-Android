////////////////////////////////////////////////////////////////////////////////////////////////////
apply plugin: 'com.android.application'
apply plugin: 'android-apt'
android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        applicationId "com.kassisdion.nestedworld"
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    productFlavors {
        dev {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
        }
        prod {
            minSdkVersion 16
        }
    }
    signingConfigs {
        release {

        }
        debug {

        }
    }
    buildTypes {
        release {
            buildConfigField "com.nestedworld.nestedworld.network.NetworkConstant.Environement", "ENVIRONMENT", "com.nestedworld.nestedworld.network.NetworkConstant.Environement.PROD"
            resValue "string", "app_name", "Nestedworld"
            buildConfigField "Boolean", "LOG", 'false'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {//Same as dev (without app_name and environement)
            buildConfigField "Boolean", "LOG", 'true'
            versionNameSuffix "-SNAPSHOT"
            signingConfig signingConfigs.debug
        }
        with_back_dev {
            initWith debug // inherit from debug (attribute and signing configurations).
            buildConfigField "com.nestedworld.nestedworld.network.NetworkConstant.Environement", "ENVIRONMENT", "com.nestedworld.nestedworld.network.NetworkConstant.Environement.DEV"
            resValue "string", "app_name", "Nestedworld Dev"
        }
        with_back_prod {
            initWith debug // inherit from debug (attribute and signing configurations).
            buildConfigField "com.nestedworld.nestedworld.network.NetworkConstant.Environement", "ENVIRONMENT", "com.nestedworld.nestedworld.network.NetworkConstant.Environement.PROD"
            resValue "string", "app_name", "Nestedworld Prod"
        }
    }
}
////////////////////////////////////////////////////////////////////////////////////////////////////
ext {
    supportLibVersion = '24.2.1'
    retrofitVersion = '2.1.0'
    okHttpVersion = '3.4.1'
    butterknifeVersion = '8.4.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    //GPS
    compile 'com.google.android.gms:play-services:9.4.0'

    //Annotation replacing findViewById()
    compile "com.jakewharton:butterknife:${butterknifeVersion}"
    apt "com.jakewharton:butterknife-compiler:${butterknifeVersion}"

    //Support library
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"

    //Material design support helper
    compile 'com.github.rey5137:material:1.2.4'

    //API
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"//send request
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"//use Json converter
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"//display log
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"

    //ORM
    //compile 'com.github.satyan:sugar:1.5'
    compile 'com.github.satyan:sugar:b16e65326a'

    //MessagePack (real time API)
    compile 'org.msgpack:msgpack-core:0.8.11'

    //Customize typeface
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'

    //Image management
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'jp.wasabeef:glide-transformations:1.3.1'//rounded image

    //Public / Subsbrice to event
    compile 'org.greenrobot:eventbus:3.0.0'

    //multi dex
    compile 'com.android.support:multidex:1.0.1'
}