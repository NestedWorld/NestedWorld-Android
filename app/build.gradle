apply plugin: 'com.android.application'
apply plugin: 'newrelic'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.newrelic.agent.android:agent-gradle-plugin:5.3.0"
    }
}

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.nestedworld.nestedworld"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //GPS
    compile 'com.google.android.gms:play-services:8.1.0'

    //Annotation replacing findViewById()
    compile 'com.jakewharton:butterknife:7.0.1'

    //Material design support
    compile 'com.android.support:design:23.0.1'
    compile 'com.github.rey5137:material:1.2.1'

    //API
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'//keep under 2.3 until new relic fix his library
    compile 'com.google.code.gson:gson:2.3'

    //New relic
    compile 'com.newrelic.agent.android:android-agent:5.3.0'
}

android.applicationVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
    }
}
